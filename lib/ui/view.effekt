// based on exercise 5 template by @jiribenes (MIT)

import ref
import string
import test

import lib/ui/node
import lib/ui/model
import lib/ui/dirty
import lib/ui/canvas
import lib/ui/vector

type Html[Ev] {
  Text(content: String);
  Canvas(classes: List[String], handler: List[EventHandler[Ev]], canvas: Canvas)
  Element(tag: String, classes: List[String], handler: List[EventHandler[Ev]], children: List[Html[Ev]], value: String)
}

type EventHandler[Ev] {
  OnClick(handler: Ev)
  OnScroll(handler: Double => Ev at {})
  OnKeyUp(handler: String => Ev at {})
  OnDrag(handler: Vector => Ev at {})
  OnResize(handler: Vector => Ev at {})
}

extern type IdleDeadline

extern def requestIdleCallback(callback: (IdleDeadline) => Unit at {io, global}) at io: Unit =
  jsWeb "window.requestIdleCallback(deadline => $effekt.runToplevel((ks, k) => ${callback}(deadline, ks, k)))"

extern def style(path: String) at io: Unit =
  jsWeb "document.head.insertAdjacentHTML('beforeend', '<link rel=\"stylesheet\" href=${path} />')"

record View[St, Ev](
  update: Ev => Dirtiness / State[St] at {io, global},
  tick: () => Dirtiness / State[St] at {io, global},
  view: () => Html[Ev] / State[St] at {io, global}
)

def run[St, Ev](root: Node, init: St, app: View[St, Ev]) = app match {
  case View(update, tick, view) =>
    val inbox = ref[List[Ev]](Nil())
    val state = ref(init)
    val dirty = ref(true)

    def send(ev: Ev): Unit = inbox.set(Cons(ev, inbox.get))

    def handleEvent(element: Node, handler: List[EventHandler[Ev]]) = {
      handler.foreach {
        case OnClick(ev) =>
          element.onClick(box { send(ev) })
        case OnScroll(ev) =>
          element.onScroll(box { delta => send(ev(delta)) })
        case OnKeyUp(ev) =>
          element.onKeyUp(box { key => send(ev(element.value)) })
        case OnDrag(ev) =>
          element.onDrag(box { (x, y) => send(ev(Vector(x, y))) })
        case OnResize(ev) =>
          element.onResize(box { (initial, width, height) =>
            if (not(initial)) send(ev(Vector(width, height)))
          })
      }
    }

    // renders the Node to the DOM
    def render(html: Html[Ev]): Node = html match {
      case Text(content) => createTextNode(content)
      case Element(tag, classes, handler, children, value) =>
        val el = createElement(tag)
        el.setValue(value)
        el.handleEvent(handler)
        classes.foreach { class => el.addClass(class); () }
        children.foreach { child =>
          el.appendChild(child.render)
          ()
        }
        el
      case Canvas(classes, handler, canvas) => {
        val el = createElement("canvas")
        canvas.applyTo(el)
        el.handleEvent(handler)
        classes.foreach { class => el.addClass(class); () }
        el
      }
    }

    // renders the app with the current state
    def render(): Unit = {
      val rendered = statefully(state) { view() }.render
      if (dirty.get()) {
        root.clear;
        root.appendChild(rendered); ()
      }
      dirty.set(false)
      ()
    }

    def loop(deadline: IdleDeadline): Unit = {
      val messages = inbox.get.reverse
      inbox.set(Nil())

      if (messages.nonEmpty) {
        messages.foreach { ev =>
          with statefully(state)
          if (update(ev) is Dirty()) dirty.set(true)
        }

        render()
      }
      // statefully(state) { tick() }
      requestIdleCallback(box loop)
    }

    render()
    requestIdleCallback(box loop)
}
