import map

import lib/ui/geometry
import lib/ui/vector
import lib/language/ic/term
import lib/net/net

def circularLayout(k: Int): Vector = {
  val scale = 50.0 * 1.1.pow(k / 10)
  Vector((k.mod(3) + 1).toDouble * scale, (k.mod(3) + 1).toDouble * scale).rotate(k.toDouble)
}

def net!(): Unit / { Exception[WrongFormat], read[Constructor], NetStream } = {
  var ids: Label = 0
  var map: Map[String, Label] = emptyGeneric()

  def uniqueMatch(port: Port) = {
    map.get(port.name) match {
      case Some(id) =>
        if (port.pol is Pos()) do emit(Wire(id + 1, id))
        if (port.pol is Neg()) do emit(Wire(id, id + 1))
        map = map.delete(port.name)
        id + 1
      case None() =>
        map = map.put(port.name, ids)
        ids = ids + 2
        ids - 2
    }
  }

  var k = 0

  with exhaustively
  val ctor = do read()
  val pp_ = ctor.pp.uniqueMatch()
  val aux_ = ctor.aux.map { p => p.uniqueMatch() }
  println("${k.show}: ${circularLayout(k).x.show},${circularLayout(k).y.show}")
  do emit(Agent(ctor.pp.name, pp_, aux_, ctor.kind, circularLayout(k)))
  k = k + 1
}
