import map

import lib/ui/geometry
import lib/ui/vector
import lib/language/ic/term
import lib/net/net

def circularLayout(k: Int): Vector = {
  Vector(k.mod(3).toDouble, k.mod(3).toDouble).rotate(k.toDouble)
}

def net!(): Unit / { Exception[WrongFormat], read[Constructor], NetStream } = {
  var ids: Label = 0
  var map: Map[String, Label] = emptyGeneric()

  def uniqueMatch(port: Port) = {
    map.get(port.name) match {
      case Some(id) =>
        if (port.pol is Pos()) do emit(Wire(id + 1, id))
        if (port.pol is Neg()) do emit(Wire(id, id + 1))
        map = map.delete(port.name)
        id + 1
      case None() =>
        map = map.put(port.name, ids)
        ids = ids + 2
        ids - 2
    }
  }

  var k = 0
  exhaustively {
    val ctor = do read()
    val pp_ = ctor.pp.uniqueMatch()
    val aux_ = ctor.aux.map { p => p.uniqueMatch() }
    // do emit(Agent(ctor.pp.name, pp_, aux_, ctor.kind, circularLayout(k)))
    do emit(Agent(ctor.pp.name, pp_, aux_, ctor.kind, Vector(0.0, 0.0)))
    k = k + 1
  }
}