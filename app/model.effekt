import map

import app/interactive
import lib/net/net

import lib/ui/model
import lib/language/ic/parser
import lib/language/ic/compiler

import lib/language/ic/term

type Mode { SourceLanguage(); LambdaCalculus(); InteractionCalculus() }

type TextStream = => Unit / emit[Char] at {global}
record EvaluatorState(mode: Mode, sources: Map[Mode, TextStream], net: NetState, errors: String)

interface Model {
  def render(): Unit
  def step(): Unit
  def nextMode(): Unit
  def handleCanvas(): Unit
  def setSource(s: String): Unit

  def getMode(): Mode
  def getSource(mode: Mode): TextStream
  def getErrors(): String
}

def show(m: Mode) = m match {
  case SourceLanguage()      => "Source Language"
  case LambdaCalculus()      => "Lambda Calculus"
  case InteractionCalculus() => "Interaction Calculus"
}

def emptyEvaluatorState() = {
  var map = map::emptyGeneric[Mode, TextStream]()
  val emptyStream: => Unit / emit[Char] at {} = box{}
  map = map.put(SourceLanguage(), emptyStream)
  map = map.put(LambdaCalculus(), emptyStream)
  map = map.put(InteractionCalculus(), emptyStream)
  // EvaluatorState(SourceLanguage(), map, "")
  EvaluatorState(InteractionCalculus(), map, emptyNetState(), "")
}

def report[E](proxy: on[E], rollback: EvaluatorState) { prog: => EvaluatorState / Exception[E] }: EvaluatorState =
  try { prog() } with Exception[E] {
    def raise(exception: E, msg: String) = {
      println(msg)
      EvaluatorState(rollback.mode, rollback.sources, rollback.net, msg)
    }
  }

def nextMode(state: EvaluatorState): EvaluatorState = state match {
  case EvaluatorState(SourceLanguage(), sources, net, errors)      => EvaluatorState(LambdaCalculus(), sources, net, errors)
  case EvaluatorState(LambdaCalculus(), sources, net, errors)      => EvaluatorState(InteractionCalculus(), sources, net, errors)
  case EvaluatorState(InteractionCalculus(), sources, net, errors) => EvaluatorState(SourceLanguage(), sources, net, errors)
}

def setSource(state: EvaluatorState, s: String): EvaluatorState = state match {
  case EvaluatorState(mode, sources, net, errors) => {
    // this would have been so cool but would only work for linear ASCII input (no escape, arrow keys)
    // val currentStream = sources.getOrElse(mode){<>}
    // val newStream = box { do emit(c); currentStream() }
    EvaluatorState(mode, sources.put(mode, box { s.each }), net, errors)
  }
}

def render(state: EvaluatorState): EvaluatorState = state match {
  case EvaluatorState(mode, sources, net, errors) => {
    with on[WrongFormat].report(state)
    with returning::source[Char, EvaluatorState] { sources.getOrElse(mode) { panic("invalid mode") }() }

    val net_ = loadNet { compile!() }
    EvaluatorState(mode, sources, net_, "")
  }
}

def model[R] { prog: => R / Model }: R / State[EvaluatorState] =
  try { prog() }
  with Model {
    def render() = resume(modifyState { st => st.render })
    def step() = <>
    def nextMode() = resume(modifyState { st => st.nextMode })
    def setSource(c) = resume(modifyState { st => st.setSource(c) })
    def handleCanvas() = <>

    def getMode() = resume((do getState()).mode)
    def getSource(mode) = {
      val state = do getState()
      val source = state.sources.getOrElse(mode) { panic("invalid mode") }
      resume(source)
    }
    def getErrors() = resume((do getState()).errors)
  }
