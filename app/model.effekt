import map

import app/net
import lib/net/net

import lib/ui/model
import lib/ui/vector
import lib/language/ic/parser
import lib/language/ic/compiler

import lib/language/ic/term

type Mode { SourceLanguage(); LambdaCalculus(); InteractionCalculus() }

type TextStream = => Unit / emit[Char] at {global}
record EvaluatorState[N](mode: Mode, sources: Map[Mode, TextStream], net: N, errors: String)

def map[A, B](state: EvaluatorState[A]) { f: A => B }: EvaluatorState[B] = state match {
  case EvaluatorState(mode, sources, net, errors) => {
    EvaluatorState(mode, sources, f(net), errors)
  }
}

def modifyState[N] { f: N => N }: Unit / State[EvaluatorState[N]] =
  do setState((do getState()).map { net => f(net) })

interface Model[C] {
  def render(): Dirtiness
  def stepReduction(): Dirtiness
  def stepAnimation(): Dirtiness
  def nextMode(): Dirtiness
  def setSource(s: String): Dirtiness
  def setSize(v: Vector): Dirtiness // TODO: leaking impl

  def getMode(): Mode
  def getSource(mode: Mode): TextStream
  def getContext(): C
  def getErrors(): String
}

def show(m: Mode) = m match {
  case SourceLanguage()      => "Source Language"
  case LambdaCalculus()      => "Lambda Calculus"
  case InteractionCalculus() => "Interaction Calculus"
}

def empty[N, C](): EvaluatorState[N] / NetModel[N, C] = {
  var map = map::emptyGeneric[Mode, TextStream]()
  val emptyStream: => Unit / emit[Char] at {} = box{}
  map = map.put(SourceLanguage(), emptyStream)
  map = map.put(LambdaCalculus(), emptyStream)
  map = map.put(InteractionCalculus(), emptyStream)
  // EvaluatorState(SourceLanguage(), map, empty(), "")
  EvaluatorState(InteractionCalculus(), map, do empty(), "")
}

def report[E, N](proxy: on[E], rollback: EvaluatorState[N]) { prog: => EvaluatorState[N] / Exception[E] }: EvaluatorState[N] =
  try { prog() } with Exception[E] {
    def raise(exception: E, msg: String) = {
      println(msg)
      EvaluatorState(rollback.mode, rollback.sources, rollback.net, msg)
    }
  }

def nextMode[N](state: EvaluatorState[N]): EvaluatorState[N] = state match {
  case EvaluatorState(SourceLanguage(), sources, net, errors)      => EvaluatorState(LambdaCalculus(), sources, net, errors)
  case EvaluatorState(LambdaCalculus(), sources, net, errors)      => EvaluatorState(InteractionCalculus(), sources, net, errors)
  case EvaluatorState(InteractionCalculus(), sources, net, errors) => EvaluatorState(SourceLanguage(), sources, net, errors)
}

def setSource[N](state: EvaluatorState[N], s: String): EvaluatorState[N] = state match {
  case EvaluatorState(mode, sources, net, errors) => {
    // this would have been so cool but would only work for linear ASCII input (no escape, arrow keys)
    // val currentStream = sources.getOrElse(mode){<>}
    // val newStream = box { do emit(c); currentStream() }
    EvaluatorState(mode, sources.put(mode, box { s.each }), net, errors)
  }
}

// compile + load + render
def render[N, C](state: EvaluatorState[N]): EvaluatorState[N] / NetModel[N, C] =
  with on[MissingValue].panic // should be impossible by now
  with on[WrongFormat].report(state)
  with returning::source[Char, EvaluatorState[N]] {
    (state.sources.get(state.mode).value)()
  }
  state match {
    case EvaluatorState(InteractionCalculus(), sources, net, errors) => {
      val net_ = do render(do load(net) { compile!() })
      EvaluatorState(InteractionCalculus(), sources, net_, "")
    }
    case _ => <>
}

def model[C, N, R] { prog: => R / Model[C] }: R / { State[EvaluatorState[N]], NetModel[N, C] } =
  with on[MissingValue].panic // should be impossible by now

  try { prog() }
  with Model[C] {
    def render() = { modifyState { st => st.render }; resume(Redraw()) }
    def stepReduction() = <>
    def stepAnimation() = { modifyState { net => do render(net) }; resume(Redraw()) }
    def nextMode() = { modifyState { st => st.nextMode }; resume(Redraw()) }
    def setSource(c) = { modifyState { st => st.setSource(c) }; resume(Keep()) }
    def setSize(v) = { modifyState { net => do setSize(net, v) }; resume(Keep()) }

    def getMode() = resume((do getState()).mode)
    def getSource(mode) = {
      val state = do getState()
      val source = state.sources.get(mode).value
      resume(source)
    }
    def getErrors() = resume((do getState()).errors)
    def getContext() = resume(do context((do getState()).net))
  }
