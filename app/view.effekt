import lib/ui/view
import lib/ui/node

import app/model

def text[Ev](content: String) = Text[Ev](content)

def button[Ev](handler: List[EventHandler[Ev]], classes: List[String], children: List[Html[Ev]]) =
  Element("button", classes, handler, children, "")

def div[Ev](handler: List[EventHandler[Ev]], classes: List[String], children: List[Html[Ev]]) =
  Element("div", classes, handler, children, "")

def div[Ev](classes: List[String], children: List[Html[Ev]]): Html[Ev] =
  div([], classes, children)

def canvas[Ev](handler: List[EventHandler[Ev]], classes: List[String]): Html[Ev] =
  Element("canvas", classes, handler, [], "")

def textarea[Ev](handler: List[EventHandler[Ev]], classes: List[String], text: TextStream): Html[Ev] =
  Element("textarea", classes, handler, [], collect { text() })

// ---

type Event {
  Render();
  Step();
  NextMode();
  HandleCanvas();
  SetSource(s: String);
}

def dispatch(msg: Event) = msg match {
  case Render()       => do render()
  case Step()         => do step()
  case NextMode()     => do nextMode()
  case HandleCanvas() => do handleCanvas()
  case SetSource(s)   => do setSource(s)
}

def view(): Html[Event] / Model = {
  div([ "grid" ], [
    div([ "menu" ], [
      text("Nette Effekte"),
      button([ OnClick(NextMode(), Redraw()) ], [], [ text("Current Mode: " ++ show(do getMode())) ]),
      button([ OnClick(Render(), Redraw())], [], [ text("Render") ]),
      button([ OnClick(Step(), Redraw()) ], [], [ text("Step") ]),
    ]),
    div([ "split" ], [
      textarea([ OnKeyUp(box { s => SetSource(s) }, Keep()) ], ["left", "code"], do getSource(do getMode())),
      canvas([ OnClick(HandleCanvas(), Redraw()) ], ["right", "inet"]),
    ]),
    div([ "errors" ], [ text(do getErrors()) ]),
  ])
}

def start() = {
  style("../app/style.css")

  val appDiv = getElementById("app").getOrElse {
    val newDiv = createElement("div").setAttribute("id", "app")
    documentBody.appendChild(newDiv)
  }

  run(appDiv, emptyEvaluatorState(), View(
    update = box { (ev) =>
      with model
      ev.dispatch()
    },
    view = box {
      with model
      view()
    }))
}