import lib/ui/view
import lib/ui/node
import lib/ui/canvas
import lib/ui/vector

import app/model

def text[Ev](content: String) = Text[Ev](content)

def button[Ev](handler: List[EventHandler[Ev]], classes: List[String], children: List[Html[Ev]]) =
  Element("button", classes, handler, children, "")

def div[Ev](handler: List[EventHandler[Ev]], classes: List[String], children: List[Html[Ev]]) =
  Element("div", classes, handler, children, "")

def div[Ev](classes: List[String], children: List[Html[Ev]]): Html[Ev] =
  div([], classes, children)

def canvas[Ev](handler: List[EventHandler[Ev]], classes: List[String], canvas: Canvas): Html[Ev] =
  Canvas(classes, handler, canvas)

def textarea[Ev](handler: List[EventHandler[Ev]], classes: List[String], text: TextStream): Html[Ev] =
  Element("textarea", classes, handler, [], collect { text() })

// ---

type Event {
  Render();
  StepReduction();
  StepAnimation();
  NextMode();
  SetSource(s: String);
  SetCanvasSize(v: Vector);
}

def dispatch(msg: Event) = msg match {
  case Render()         => do render()
  case StepReduction()  => do stepReduction()
  case StepAnimation()  => do stepAnimation()
  case NextMode()       => do nextMode()
  case SetSource(s)     => do setSource(s)
  case SetCanvasSize(v) => do setCanvasSize(v)
}

def view(): Html[Event] / Model = {
  div([ "grid" ], [
    div([ "menu" ], [
      text("Nette Effekte"),
      button([ OnClick(NextMode(), Redraw()) ], [], [ text("Current Mode: " ++ show(do getMode())) ]),
      button([ OnClick(Render(), Redraw())], [], [ text("Render") ]),
      button([ OnClick(StepReduction(), Redraw()) ], [], [ text("Step") ]),
    ]),
    div([ "split" ], [
      textarea([ OnKeyUp(box { s => SetSource(s) }, Keep()) ], ["left", "code"], do getSource(do getMode())),
      canvas([ OnResize(box { v => SetCanvasSize(v) }, Keep()), OnClick(StepAnimation(), Redraw()) ], ["right", "inet"],
        do getCanvas()),
    ]),
    div([ "errors" ], [ text(do getErrors()) ]),
  ])
}

def start() = {
  style("../app/style.css")

  val appDiv = getElementById("app").getOrElse {
    val newDiv = createElement("div").setAttribute("id", "app")
    documentBody.appendChild(newDiv)
  }

  run(appDiv, model::empty(), View(
    update = box { (ev) =>
      with model
      ev.dispatch()
    },
    tick = box {
      with model
      StepAnimation().dispatch()
    },
    view = box {
      with model
      view()
    }))
}