import lib/ui/view
import lib/ui/dirty
import lib/ui/node
import lib/ui/canvas
import lib/ui/vector

import app/interactive/model
import app/model

def text[Ev](content: String) = Text[Ev](content)

def button[Ev](handler: List[EventHandler[Ev]], classes: List[String], children: List[Html[Ev]]): Html[Ev] / tag =
  Element("button", classes, handler, children, "").tag!

def div[Ev](handler: List[EventHandler[Ev]], classes: List[String], children: List[Html[Ev]]): Html[Ev] / tag =
  Element("div", classes, handler, children, "").tag!

def div[Ev](classes: List[String], children: List[Html[Ev]]): Html[Ev] / tag =
  div([], classes, children).tag!

def canvas[Ev](handler: List[EventHandler[Ev]], classes: List[String], canvas: Canvas): Html[Ev] / tag =
  Canvas(classes, handler, canvas).tag!

def textarea[Ev](handler: List[EventHandler[Ev]], classes: List[String], text: TextStream): Html[Ev] / tag =
  Element("textarea", classes, handler, [], collect { text() }).tag!

// ---

type Event {
  Typing(y: Bool);
  Render();
  StepReduction();
  StepAnimation();
  NextMode();
  SetSource(s: String);
  SetCanvasSize(v: Vector);
  PanCanvas(v: Vector);
  ZoomCanvas(d: Double);
}

def dispatch(msg: Event): Unit / { Model[Canvas, CanvasOp], Redraw }  = msg match {
  case Typing(y)        => do typing(y)
  case Render()         => do render()
  case StepReduction()  => do stepReduction()
  case StepAnimation()  => do stepAnimation()
  case NextMode()       => do nextMode()
  case SetSource(s)     => do setSource(s)
  case SetCanvasSize(v) => do onContext(Size(v))
  case PanCanvas(v)     => do onContext(Pan(v))
  case ZoomCanvas(d)    => do onContext(Zoom(d))
}

def view(): Html[Event] / { Model[Canvas, CanvasOp], tag } = {
  div([ "grid" ], [
    div([ "menu" ], [
      text("Nette Effekte"),
      button([ OnClick(NextMode()) ], [], [ text("Current Mode: " ++ show(do getMode())) ]),
      button([ OnClick(Render())], [], [ text("Render") ]),
      button([ OnClick(StepReduction()) ], [], [ text("Step") ]),
    ]),
    div([ "split" ], [
      textarea([
        OnKeyUp(box { s => SetSource(s) }),
        OnClick(Typing(true))
      ], ["left", "code"], do getSource(do getMode())),
      canvas([
        OnResize(box { v => SetCanvasSize(v) }),
        OnDrag(box { v => PanCanvas(v) }),
        OnScroll(box { d => ZoomCanvas(d) }),
        OnTick(StepAnimation()),
        // OnMouseDown(Typing(true)), // TODO: to prevent panning lag
        OnClick(Typing(false)),
      ], ["right", "inet"], do getContext()),
    ]),
    div([ "errors" ], [ text(do getErrors()) ]),
  ])
}

def start() = {
  style("../app/style.css")

  val appDiv = getElementById("app").getOrElse {
    val newDiv = createElement("div").setAttribute("id", "app")
    documentBody.appendChild(newDiv)
  }

  val lastTime = ref(0)

  with interactive
  run(appDiv, model::empty[NetState, Canvas, CanvasOp](), View(
    update = box { ev =>
      with interactive
      with model[Canvas, CanvasOp, Unit, NetState]
      ev.dispatch()
    },
    view = box {
      with interactive
      with model[Canvas, CanvasOp, Html[Event], NetState]
      view()
    }))
}